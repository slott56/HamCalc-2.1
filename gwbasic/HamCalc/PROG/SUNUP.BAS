10 :REM' SUNUP - 29 SEP 86 rev. 23 NOV 03. 'excerpted from PATHFIND.BAS
20 CLS:KEY OFF
30 COMMON
40 COLOR 7,0,0
50 IF EX$=""THEN EX$="EXIT"
60 DIM M$(12),D(12),D$(12)
70 AM$="##:## am":AZ$=" ###.#°":PM$="##:## pm":EL$=" ##.#°"
80 P$="##.##":P1$="####":P2$="##.#"
90 UL$=STRING$(80,205)
100 PI=4*ATN(1)
110 DR=PI/180:RD=180/PI         :REM'DEG to RAD, RAD to DEG
120 :REM'
130 CLS
140 PRINT "SUNUP/SUNDOWN";TAB(57)"by George Murphy VE3ERP";
150 PRINT UL$;
160 PRINT "Press number in ( ) to:"
170 PRINT " (1)  Run program"
180 PRINT " (0)  Exit"
190 Z$=INKEY$:IF Z$=""THEN 190
200 IF Z$="0"THEN CLS:CHAIN EX$
210 IF Z$="1"THEN 230 :ELSE 190
220 GOTO 190
230 VIEW PRINT 3 TO 24:CLS:VIEW PRINT:LOCATE 3
240 LINE INPUT "Name of your location.......? ";N$
250 INPUT "ENTER: Your latitude  (XX.X degrees, minus if SOUTH).....";LX
260 OO=LX:GOSUB 1600
270 INPUT "ENTER: Your longitude (XX.X degrees, minus if EAST)......";LO
280 OO=LO:GOSUB 1600
290 LA=LX*DR     :REM'Latitude in Radians
300 LN=CSRLIN:PRINT "Press number in ( ) to indicate your Time Zone:"
310 PRINT " (1) Atlantic"
320 PRINT " (2) Eastern"
330 PRINT " (3) Central"
340 PRINT " (4) Mountain"
350 PRINT " (5) Pacific"
360 PRINT " (6) Other"
370 Z$=INKEY$:IF VAL(Z$)>=1 AND VAL(Z$)<=6 THEN 380 :ELSE 370
380 IF Z$="1"THEN SX=60:SX$="AST"
390 IF Z$="2"THEN SX=75:SX$="EST"
400 IF Z$="3"THEN SX=90:SX$="CST"
410 IF Z$="4"THEN SX=105:SX$="MST"
420 IF Z$="5"THEN SX=120:SX$="PST"
430 IF Z$="6"THEN SX=INT(LO/15)*15:SX$="Local"
440 VIEW PRINT LN TO 24:CLS:VIEW PRINT:LOCATE LN
450 MX=LO-SX                                :REM'Deg. correction for std. meridian
460 MC=MX/15                                :REM'Hrs. correction for std. meridian
470 MR=MX*DR                                :REM'Rad equiv. of meridian correction
480 INPUT "ENTER: Year to be used in calculations (yyyy)............";YR
490 OO=YR:GOSUB 1600:GOSUB 1600:Z=(YR/100-INT(YR/100))*10
500 Y=INT(Z):X=(Z-Y)*10:W=(Y+X)/4:W=INT(W*100000.0!+0.5!)/100000.0!
510 IF INT(W)=W THEN Q1=29:DA=366 :ELSE Q1=28:DA=365.2423095703125!
520 DATA JAN,31,FEB,0,MAR,31,APR,30,MAY,31,JUN,30
530 DATA JUL,31,AUG,31,SEP,30,OCT,31,NOV,30,DEC,31
540 FOR Z=1 TO 12:READ M$(Z),D$(Z):D(Z)=VAL(D$(Z)):NEXT Z:RESTORE:D(2)=Q1
550 K=360/DA                                :REM'Days to degrees
560 :REM'
570 N1=0:N2=0:N3=0:N4=0:YZ=1
580 PRINT "ENTER: First date? (No. of Month, Day)...................";
590 INPUT M1,D1:GOSUB 1570:MO1=M1
600 N1=NQ
610 PRINT "ENTER: Last date?  (No. of Month, Day)...................";
620 INPUT M1,D1:GOSUB 1570:MO2=M1
630 N2=NQ:IF N2<N1 THEN N2=365+N2
640 :REM'
650 :REM'.....sunrise/sunset
660 INPUT "ENTER: Increment in days.................................";X1
670 IF X1=0 THEN X1=1
680 CLS
690 LOCATE 1,1
700 PRINT TAB(14)"Hours"TAB(24)"Sunrise (";SX$;")";
710 PRINT TAB(45)"Sunset (";SX$;")";TAB(65)"Elev.& Azimuth"
720 PRINT TAB(14)"of";
730 PRINT TAB(24)"and Azimuth"TAB(45)"and Azimuth"TAB(66)"of Noon Sun"
740 PRINT "   Date"TAB(14)"Daylight"TAB(24)" (degrees)";
750 PRINT TAB(45)" (degrees)"TAB(66)" (degrees)"
760 PRINT UL$;
770 VIEW PRINT 5 TO 23:CLS:VIEW PRINT:LOCATE 5
780 GOSUB 1660
790 LN=4
800 FOR N=N1 TO N2 STEP X1
810 LN=LN+1
820 GOSUB 1320
830 CC=PI/2                 :REM'90 degrees at sunrise, sunset
840 IF LA-Z<0 THEN T=(ED-MG+MX+0.5!):ELSE  T=(180-ED-MG-MX-0.5!)
850 IF T<0 THEN T=360+T
860 AA=CC-Z
870 BB=CC-LA
880 SS=(AA+BB+CC)/2
890 TT=(SIN(SS-AA)*SIN(SS-BB)*SIN(SS-CC))/SIN(SS)
900 IF TT<0 THEN TT=0
910 TR=SQR(TT)
920 C1=TR/SIN(SS-CC)
930 C=2*ATN(C1)*RD                :REM'Angle in degrees
940 AZ=2*RD*ATN(TR/SIN(SS-AA))    :REM'Azimuth in degrees
950 CH=C/15                       :REM'Hours vs. C
960 :REM'.....sunrise
970 R=12-0.0560000017285347!+ET+MC-CH:H1    =R   :REM'Sunrise
980 R$=STR$(INT(R)):RL=LEN(R$)-1
990 RH$=RIGHT$((R$),RL)           :REM'sunrise decimal hour
1000 IF VAL(RH$)<10 THEN RH$="0"+RH$
1010 M=INT((R-INT(R))*60)
1020 RM$=STR$(M):ML=LEN(RM$)-1
1030 RM$=RIGHT$((RM$),ML)          :REM'sunrise decimal minute
1040 IF VAL(RM$)<10 THEN RM$="0"+RM$
1050 RT$=RH$+":"+RM$               :REM'sunrisetime
1060 :REM'.....sunset
1070 S=12+0.0560000017285347!+ET+MC+CH:H2=S  :REM'Sunset
1080 S$=STR$(INT(S)):SL=LEN(S$)-1
1090 SH$=RIGHT$((S$),SL)           :REM'sunset decimal hour
1100 IF VAL(SH$)<10 THEN SH$="0"+SH$
1110 M=INT((S-INT(S))*60)
1120 SM$=STR$(M):ML=LEN(SM$)-1
1130 SM$=RIGHT$((SM$),ML)          :REM'sunset decimal minute
1140 IF VAL(SM$)<10 THEN SM$="0"+SM$
1150 ST$=SH$+":"+SM$               :REM'sunset time
1160 IF LA-Z<0 THEN AL=FIX(90-(Z-LA)*RD-0.5!):ELSE AL=(90-(LA-Z)*RD-0.5!)
1170 GOSUB 1460
1180 RISE=AZ-MG-0.5!
1190 SET=360-AZ-MG-0.5!
1200 PRINT TAB(15)USING "##.#";S-R; :REM'SD-SU;
1210 PRINT TAB(24)RT$; " @";        :REM' USING AM$;SU,MU;:PRINT " @";
1220 PRINT USING AZ$;RISE;
1230 PRINT TAB(45)ST$;" @";
1240 PRINT USING AZ$;(360-AZ-MG-0.5!);
1250 PRINT TAB(65)USING "##.###°";AL;
1260 PRINT "  ";USING  "###.#°";T;
1270 IF LN<24 THEN PRINT "":GOTO 1290
1280 GOSUB 1770:CLS:LN=0
1290 NEXT N
1300 GOTO 1740   :REM'end
1310 :REM'
1320 :REM'.....compute long.of GP SUN & EQ TIME
1330 NN=N:IF N>365 THEN NN=N-365
1340 L1=(279.57501220703125!+(K*NN))*DR
1350 G1=(356.9670104980469!+(K*NN))*DR
1360 LD=L1+(1.9160000085830688!*SIN(G1)+0.019999999552965164!*SIN(2*G1))*DR
1370 DL=0.39781999588012695!*SIN(LD)
1380 Z=ATN(DL/SQR(-DL*DL+1))
1390 EL=-104.69999694824219!*SIN(L1)+596.2000122070312!*SIN(2*L1)+4.300000190734863!*SIN(3*L1)-12.699999809265137!
1400 EL=EL*SIN(4*L1)-429.29998779296875!*COS(L1)-2*COS(2*L1)+19.299999237060547!*COS(3*L1)
1410 ET=-EL/3600
1420 ED=ET*15
1430 ER=ED*DR
1440 RETURN
1450 :REM'
1460 :REM'.....print month and day
1470 NN=N:IF N>365 THEN NN=N-365
1480 Q1=0:FOR Q=1 TO 12:Q2=Q1:Q1=Q1+D(Q):IF NN<=Q1 THEN 1500
1490 NEXT Q
1500 PRINT M$(Q);" ";
1510 A=NN-Q2:A$=STR$(A):M=LEN(A$)-1
1520 A$=RIGHT$((A$),M)
1530 IF VAL(A$)<10 THEN A$="0"+A$
1540 PRINT A$;"/";YR$(YZ);
1550 RETURN
1560 :REM'
1570 :REM'.....convert date to day of year
1580 Q1=0:FOR Q=1 TO M1:Q2=Q1:Q1=Q1+D(Q):NEXT Q:NQ=Q2+D1:RETURN
1590 :REM'
1600 :REM'......subsequent years
1610 Y$(1)=STR$(YR):Y$(2)=STR$(YR+1)
1620 IF MO1=MO2 THEN Y$(2)=Y$(1)
1630 FOR Q=1 TO 2:YR$(Q)=RIGHT$(Y$(Q),2):NEXT Q
1640 RETURN
1650 :REM'
1660 VIEW PRINT:LOCATE 4,1
1670 IF LX>0 THEN LX$="°N.":ELSE LX$="°S."
1680 IF LO>0 THEN LO$="°W.":ELSE LO$="°E."
1690 LOCATE ,13:PRINT "╡at "N$" ("STR$(ABS(LX))LX$STR$(ABS(LO))LO$")╞═"
1700 LOCATE CSRLIN-1,64:PRINT " EL "
1710 LOCATE CSRLIN-1,74:PRINT " AZ "
1720 RETURN
1730 :REM'
1740 :REM'.....end
1750 GOSUB 1770:GOTO 130
1760 :REM'
1770 :REM'.....PRT
1780 KEY OFF:GOSUB 1850:LOCATE 25,5:COLOR 0,2
1790 PRINT " Send this page to:(1)Printer Queue? (2)Printout? ";
1800 PRINT "(3)Next page? (1/2/3)";:COLOR 7,0
1810 Z$=INKEY$:IF Z$<"1"OR Z$>"3"THEN 1810 :ELSE GOSUB 1850
1820 IF Z$="3"THEN RETURN
1830 FOR I%=1 TO 24:FOR J%=1 TO 80:LPRINT CHR$(SCREEN(I%,J%));:NEXT J%:NEXT I%
1840 IF Z$="2"THEN LPRINT CHR$(12) :ELSE 1780
1850 LOCATE 25,1:PRINT STRING$(80,32);:RETURN
