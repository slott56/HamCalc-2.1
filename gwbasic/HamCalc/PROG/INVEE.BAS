10 :REM'INVEE - inverted vee antenna dimensions - 20 DEC 93 rev. 09 HAN 2002
20 IF EX$=""THEN EX$="EXIT"
30 IF PROG$=""THEN GO$=EX$ :ELSE GO$=PROG$
40 COMMON EX$,PROG$
50 CLS:KEY OFF
60 COLOR 7,0,1
70 UL$=STRING$(80,205)
80 U$="###.##"
90 V$="####.#"
100 PI=3.1415929794311523!
110 T$=" INVERTED VEE ANTENNA DIMENSIONS"+SPACE$(24)+"by George Murphy VE3ERP "
120 :REM'
130 :REM'.....start
140 CLS:T=0:RA=0:HT=0:AN=0:MA=0:SU=0:AA=0:BB=0
150 IF PROG$="g5rv"THEN RA=51
160 IF PROG$="es2b"THEN RA=A/2
170 ROW=3
180 COL=39:X=1
190 LOCATE ROW,COL:PRINT "/║\"        :REM'draw apex
200 :REM'.....draw antenna
210 FOR Z=ROW TO ROW+15
220 X=X+2
230 IF X<=28 THEN PRINT TAB(COL+1-X);"/";
240 IF X>28 THEN PRINT TAB(COL+1-28);"║";
250 PRINT TAB(COL+1);"║";
260 IF X>28 THEN PRINT TAB(COL+1+28);"║";
270 IF X<=28 THEN PRINT TAB(COL+1+X);"\"
280 NEXT Z
290 :REM'.....draw ground line
300 PRINT TAB(6);STRING$(69,196)
310 LOCATE Z+1,COL+1:PRINT "╨"
320 LOCATE Z+1,COL-27:PRINT "╨"
330 LOCATE Z+1,COL+29:PRINT "╨"
340 :REM'.....draw dimension lines
350 LOCATE Z+3,COL-27:PRINT "│«";STRING$(53,196);"»│"
360 LOCATE Z+3,COL:PRINT "»│«"
370 LOCATE Z+3,COL-14:PRINT " B "
380 LOCATE Z+3,COL+14:PRINT " B "
390 LOCATE Z+4,COL-27:PRINT "│«";STRING$(53,196);"»│"
400 LOCATE Z+4,COL:PRINT " A "
410 :REM'.....add text
420 LOCATE ROW+2,COL-3:PRINT"«-ANGLE-»"
430 LOCATE ROW+6,COL-16:PRINT "RADIATOR"
440 LOCATE ROW+6,COL+10:PRINT "RADIATOR"
450 LOCATE ROW+8,COL-1:PRINT "TOWER"
460 LOCATE ROW+15,COL-30:PRINT "SUPPORT"
470 LOCATE ROW+15,COL+26:PRINT "SUPPORT"
480 COLOR 15,2
490 IF PROG$="g5rv"THEN 540
500 LOCATE 1:PRINT T$;
510 COLOR 1,0:PRINT STRING$(80,223);
520 COLOR 15,2
530 GOTO 550
540 LOCATE 21,26:PRINT " ALL DIMENSIONS ARE IN FEET ";:GOTO 590
550 LOCATE 24,10
560 PRINT " ENTER ALL DIMENSIONS IN SAME UNITS (e.g. metres/feet, etc.) ";
570 LOCATE 25,10:COLOR 15,2
580 PRINT "  RESULTING CALCULATED DIMENSIONS WILL BE IN THE SAME UNITS  ";
590 COLOR 7,0
600 LOCATE 2,22
610 COLOR 0,7:PRINT " Press 1 to continue or 0 to EXIT.....":COLOR 7,0
620 Z$=INKEY$:IF Z$=""THEN 620
630 IF Z$="0"THEN CLS:CHAIN GO$
640 IF Z$="1"THEN 660
650 GOTO 620
660 VIEW PRINT 1 TO 2:CLS:VIEW PRINT:LOCATE 1:COLOR 0,7
670 IF RA THEN Z$="n":GOTO 720
675 :REM'IF PROG$="g5rv"THEN Z$="n":GOTO 720
680 PRINT " Want to find radiator length for a given frequency and apex";
690 PRINT " angle? (y/n) ":COLOR 7,0
700 Z$=INKEY$:IF Z$=""THEN 700
710 IF Z$="y"THEN 1580
720 IF Z$="n"THEN DM=1:GOTO 750
730 GOTO 700
740 :REM'
750 COLOR 7,0:VIEW PRINT 1 TO 2:CLS:VIEW PRINT:LOCATE 1:COLOR 0,7
760 PRINT " Enter the following value if known. If unknown, press <ENTER> "
770 COLOR 7,0
780 :REM'
790 :REM'.....input data
800 LOCATE 2
810 COLOR 0,7
820 IF RA THEN GOSUB 910
830 IF RA=0 THEN INPUT" ENTER: Length of radiators..";RA:RA=RA/DM:GOSUB 910
840 IF AN=0 THEN INPUT" ENTER: Apex angle (degrees) ";AN:T=AN/2*PI/180:GOSUB 910
850 IF SU=0 THEN INPUT" ENTER: Height of supports...";SU:SU=SU/DM:GOSUB 910
860 IF MA=0 THEN INPUT" ENTER: Height of feedpoint..";MA:MA=MA/DM:GOSUB 910
870 IF AA=0 THEN INPUT" ENTER: Dimension A..........";AA:BB=AA/2:GOSUB 910
880 IF BB=0 THEN INPUT" ENTER: Dimension B..........";BB:AA=BB*2:GOSUB 910
890 GOTO 830
900 :REM'
910 :REM'.....calculations
920 LOCATE 2:PRINT STRING$(80,219);
930 IF HT=0 THEN IF MA*SU THEN HT=MA-SU
940 FOR I=1 TO 5
950 IF RA THEN 990
960  IF HT*BB THEN RA=SQR(HT^2+BB^2):GOTO 990
970  IF T*BB THEN RA=BB/SIN(T):GOTO 990
980  IF T*HT THEN RA=HT/COS(T):GOTO 990
990 IF HT THEN 1030
1000  IF RA*BB THEN HT=SQR(RA^2-BB^2):GOTO 1030
1010  IF T*BB THEN HT=BB/TAN(T):GOTO 1030
1020  IF T*RA THEN HT=RA*COS(T):GOTO 1030
1030 IF AN THEN 1090
1040  IF BB*HT THEN T=ATN(BB/HT):GOTO 1070
1050  IF RA*BB THEN F=BB/RA:T=ATN(F/SQR(-F*F+1)):GOTO 1070
1060  IF RA*HT THEN G=HT/RA:T=-(ATN(G/SQR(-G*G+1)))+PI/2:GOTO 1070
1070   IF T>PI/2 THEN T=T-PI/2:GOTO 1070
1080   AN=T*2*180/PI:IF AN<0 THEN AN=360+AN
1090 IF MA THEN 1110
1100  IF HT*SU THEN MA=HT+SU:GOTO 1110
1110 IF SU THEN 1150
1120  IF MA*HT THEN SU=MA-HT
1130 IF SU<0 THEN MA=HT:SU=9.999999747378752e-06!
1140 GOTO 1150
1150 IF BB THEN 1170
1160  IF RA*HT THEN BB=SQR(RA^2-HT^2):AA=2*BB:GOTO 1230
1170 IF MA*SU THEN IF MA=SU THEN AN=180:MA=MA+9.999999747378752e-05!:SU=SU-9.999999747378752e-05!
1180 IF AN<>180 THEN 1230
1190  IF RA THEN BB=RA:AA=2*RA
1200  IF BB THEN RA=BB:AA=2*BB
1210  IF AA THEN BB=AA/2:AA=2*BB
1220 :REM'
1230 :REM'.....display
1240 COLOR 0,7
1250 IF RA THEN LOCATE ROW+6,16:PRINT " RADIATOR ";USING U$;RA*DM;:PRINT " Lg."
1260 IF RA THEN LOCATE ROW+6,44:PRINT " RADIATOR ";USING U$;RA*DM;:PRINT " Lg."
1270 IF AN THEN LOCATE ROW+2,36:PRINT "«-ANGLE-»"
1280 IF AN THEN LOCATE ROW+3,36:PRINT " ";USING V$;AN;:PRINT "° "
1290 IF MA THEN LOCATE ROW+0,16:PRINT " FEEDPOINT";USING U$;MA*DM;
1300 IF MA THEN PRINT " High─»"
1310 IF SU THEN LOCATE ROW+15,2:PRINT " SUPPORT";USING U$;SU*DM;:PRINT " High "
1320 IF SU THEN LOCATE ROW+15,58:PRINT " SUPPORT";USING U$;SU*DM;:PRINT " High "
1330 IF AA THEN LOCATE ROW+20,36:PRINT " ";USING U$;AA*DM;:PRINT " "
1340 IF BB THEN LOCATE ROW+19,22:PRINT " ";USING U$;BB*DM;:PRINT " "
1350 IF BB THEN LOCATE ROW+19,50:PRINT " ";USING U$;BB*DM;:PRINT " "
1360 NEXT I
1370 LOCATE 2:PRINT STRING$(80,219):LOCATE 2:COLOR 0,7
1380 IF RA*AN*MA*SU*BB*AA THEN GOSUB 1410:CLS:RUN EX$      :REM'EXIT
1390 RETURN
1400 :REM'
1410 :REM'.....end
1420 LOCATE 1:COLOR 15,2:PRINT T$;
1430 IF PROG$="g5rv"THEN LOCATE CSRLIN-1,35:PRINT "( G5RV )"
1440 COLOR 1,0:PRINT STRING$(80,223);
1450 LOCATE 24
1460 COLOR 7,0:PRINT STRING$(80,32);
1470 IF FQ=0 THEN 1550
1480 LOCATE 3,49:PRINT USING "When apex angle= ###.#° then:";AN
1490 LOCATE 4,51:PRINT "Length of dipole in metres="
1500 LOCATE 5,51:PRINT USING "####.## ÷ Freq.in MHz";K*0.30480000376701355!
1510 LOCATE 6,51:PRINT "Length of dipole in feet="
1520 LOCATE 7,51:PRINT USING "####.## ÷ Freq.in MHz";K
1530 COLOR 15,0
1540 LOCATE 24,24:PRINT USING "RESONANT FREQUENCY = ####.### MHz";FQ;
1550 COLOR 7:GOSUB 1880
1560 GOTO 130   :REM'start
1570 :REM'
1580 :REM'.....radiator length
1590 :REM'ref. Art Blick VE3AHU, The Canadian Amateur, March 1998
1600 :REM'equations developed from an AutoCad graphic plot by VE3ERP
1610 VIEW PRINT 1 TO 2:CLS:VIEW PRINT:LOCATE 1
1620 COLOR 0,7:PRINT " Want dimensions in (m)etres or (f)eet?   (m/f) "
1630 Z$=INKEY$
1640 IF Z$="m"THEN DM=0.30480000376701355!:DM$="metres":GOTO 1670
1650 IF Z$="f"THEN DM=1:DM$="feet":GOTO 1670
1660 GOTO 1630
1670 COLOR 7,0:VIEW PRINT 24 TO 24:CLS:VIEW PRINT:LOCATE 21,27+DM
1680 COLOR 15,2:PRINT " All dimensions in ";DM$;"  "
1690 COLOR 7,0:VIEW PRINT 1 TO 2:CLS:VIEW PRINT:LOCATE 1:COLOR 0,7
1700 IF FQ THEN 1730
1710 INPUT " ENTER: Frequency, in MHz........";FQ
1720 IF FQ=0 THEN 1690
1730 INPUT " ENTER: Apex angle, in degrees...";AN
1740 IF AN=0 THEN 1690
1750 IF AN>=180 THEN AN=179.89999389648438!
1760 TN=(180-AN)/2  :REM'declination in degrees
1770 DR=TN*PI/180   :REM'declination in rad
1780 TA=TN+12.46500015258789!   :REM'degrees w/constant added
1790 TH=TA*PI/180   :REM'rad
1800 X=9.870699882507324!/458.36700439453125!*SIN(TH)
1810 TG=ATN(X/SQR(-X*X+1))
1820 TF=PI-TG-TH
1830 K=458.36700439453125!*SIN(TF)/SIN(TH) :REM'k factor in feet
1840 RA=K/FQ/2
1850 BB=RA*COS(DR):AA=2*BB
1860 GOTO 750
1870 :REM'
1880 :REM'PRT
1890 KEY OFF:GOSUB 1960:LOCATE 25,5:COLOR 0,2
1900 PRINT " Send this page to:(1)Printer Queue? (2)Printout? ";
1910 PRINT "(3)Next page? (1/2/3)";:COLOR 7,0
1920 Z$=INKEY$:IF Z$<"1"OR Z$>"3"THEN 1920 :ELSE GOSUB 1960
1930 IF Z$="3"THEN RETURN
1940 FOR I%=1 TO 24:FOR J%=1 TO 80:LPRINT CHR$(SCREEN(I%,J%));:NEXT J%:NEXT I%
1950 IF Z$="2"THEN LPRINT CHR$(12) :ELSE 1890
1960 LOCATE 25,1:PRINT STRING$(80,32);:RETURN
