10 :REM'RESISTIV - Resistivity of Metals - 19 JAN 96 rev.12 APR 97
20 IF EX$=""THEN EX$="EXIT"
30 CLS:KEY OFF
40 COLOR 7,0,1
50 DIM M$(18,2)
60 U$="###.###"
70 UL$=STRING$(80,205)
80 E$=STRING$(80,32)
90 GOTO 190
100 :REM'
110 DATA ALUMINUM (pure), BRASS (most conductive alloy)
120 DATA BRASS (least conductive alloy), CADMIUM, CHROMIUM, COPPER (hard-drawn)
130 DATA COPPER (annealed), GOLD, IRON (pure), LEAD, NICKEL
140 DATA PHOSPHOR BRONZE (most conductive alloy)
150 DATA PHOSPHOR BRONZE (least conductive alloy)
160 DATA SILVER, STEEL (most conductive alloy), STEEL (least conductive alloy)
170 DATA TIN, ZINC
180 :REM'
190 FOR Z=1 TO 18:READ M$(Z,1):NEXT Z
200 DATA 1.6, 3.7, 4.9, 4.4, 1.8, 1.03, 1.00, 1.4, 5.68, 12.8, 5.1, 2.8, 5.4
210 DATA 0.94, 7.6, 12.7, 6.7, 3.4
220 FOR Z=1 TO 18:READ M$(Z,2):NEXT Z
230 :REM'
240 :REM'.....start
250 CLS
260 COLOR 15,2
270 PRINT " RESISTIVITY OF METALS";TAB(57)"by George Murphy VE3ERP ";
280 COLOR 1,0:PRINT STRING$(80,223);
290 COLOR 7,0
300 :REM'
310 GOSUB 840    :REM'preface
320 PRINT UL$;
330 LOCATE CSRLIN,21
340 COLOR 0,7:PRINT " Press 1 to continue or 0 to EXIT......":COLOR 7,0
350 Q$=INKEY$:IF Q$=""THEN 350
360 IF Q$="0"THEN CLS:RUN EX$
370 IF Q$="1"THEN 400
380 GOTO 350
390 :REM'
400 VIEW PRINT 3 TO 24:CLS:VIEW PRINT:LOCATE 3
410 COLOR 0,7
420 PRINT " Press a letter in < > to select a Reference Base:"
430 COLOR 7,0
440 ROW=5
450 LOCATE ROW:PRINT UL$;
460  FOR Z=1 TO 18:PRINT " < ";CHR$(96+Z);" >";
470   LOCATE CSRLIN,9:PRINT M$(Z,1);
480 IF Z=6 THEN PRINT " e.g. some types of antenna wire":GOTO 510
490 IF Z=7 THEN PRINT " e.g. most electrical wiring":GOTO 510
500 PRINT ""
510  NEXT Z
520 :REM'
530 Z$=INKEY$:IF Z$=""THEN 530
540 Y=ASC(Z$):IF Y<97 OR Y>114 THEN 530
550 VIEW PRINT 3 TO 4:CLS:VIEW PRINT:LOCATE 3
560 Y=Y-96
570 PRINT TAB(8)" Relative Resistivity Factors for Various Metals,"
580 PRINT TAB(8)" compared to";
590 COLOR 0,7:PRINT " ";M$(Y,1);" "
600 COLOR 7,0
610 W=VAL(M$(Y,2))
620  FOR Z=ROW+1 TO ROW+18
630   LOCATE Z,1:PRINT "      "
640   X=LEN(M$(Z-ROW,1))
650   LOCATE Z,9+X:PRINT STRING$(46-X,".")
660   IF Z=ROW+Y THEN COLOR 0,7 :ELSE COLOR 7,0
670   LOCATE Z,53:PRINT USING U$;VAL(M$(Z-ROW,2))/W
680   COLOR 7,0
690   GOSUB 750
700  NEXT Z
710 LOCATE ROW+Y,8:COLOR 0,7:PRINT " "+M$(Y,1)+" ";
720 COLOR 7,0
730 GOTO 790
740 :REM'
750 IF Z=ROW+ 9 THEN LOCATE ,61:PRINT "(highest resistance)"
760 IF Z=ROW+13 THEN LOCATE ,61:PRINT "(lowest resistance)"
770 RETURN
780 :REM'
790 :REM'.....end
800 GOSUB 920
810 GOTO 240
820 END
830 :REM'
840 :REM'.....preface
850 T=7
860 PRINT TAB(T);
870 PRINT "This programs computes the relative resistivity of various metals"
880 PRINT TAB(T);
890 PRINT "compared to a base reference metal of your choice."
900 RETURN
910 :REM'
920 :REM'PRT
930 KEY OFF:GOSUB 1000:LOCATE 25,5:COLOR 0,2
940 PRINT " Send this page to:(1)Printer Queue? (2)Printout? ";
950 PRINT "(3)Next page? (1/2/3)";:COLOR 7,0
960 Z$=INKEY$:IF Z$<"1"OR Z$>"3"THEN 960 :ELSE GOSUB 1000
970 IF Z$="3"THEN RETURN
980 FOR I%=1 TO 24:FOR J%=1 TO 80:LPRINT CHR$(SCREEN(I%,J%));:NEXT J%:NEXT I%
990 IF Z$="2"THEN LPRINT CHR$(12) :ELSE 930
1000 LOCATE 25,1:PRINT STRING$(80,32);:RETURN
