10 :REM'BEAMDEFL - 25 MAY 92  rev. 23 MAR 2000
20 IF EX$=""THEN EX$="EXIT"
30 COMMON EX$,LN,I,A,X$
40 IF X$=""THEN X$=" beam of any cross-section"
50 IF A THEN AR=A:A=0        :REM'area of section, if chained from last program
60 CLS:KEY OFF
70 COLOR 7,0,4
80 UL$=STRING$(80,205)
90 :REM'
100 :REM'.....start
110 T$=" DEFLECTION in BEAMS"
120 COLOR 11,1:PRINT T$;TAB(57);"by George Murphy VE3ERP ";
130 COLOR 4,0:PRINT STRING$(80,"▀");
140 Y$=" Ref: Machinery's Handbook, 21st Edition "
150 COLOR 11,4:LOCATE CSRLIN-1,(80-LEN(Y$))/2:PRINT Y$
160 COLOR 7,0
170 PRINT UL$;
180 PRINT X$;":"
190 PRINT UL$;
200 :REM'
210 CLS
220 :REM'.....get modulus of elasticity
230 PRINT " Select Modulus of Elasticity by pressing letter in < >:"
240 PRINT UL$;
250 PRINT "  < a >  ALLOY STEEL......................(   30Σ6 PSI )"
260 PRINT "  < b >  STRUCTURAL STEEL.................(   29Σ6 PSI )"
270 PRINT "  < c >  WROUGHT IRON.....................(   28Σ6 PSI )"
280 PRINT "  < d >  TYPICAL ALUMINUM EXTRUSION.......( 10.3Σ6 PSI )"
290 PRINT "  < e >  WOOD - Fir.......................( 1,76Σ4 PSI )"
300 PRINT "  < f >  WOOD - Redwood...................( 1,32Σ4 PSI )"
310 PRINT "  < g >  WOOD - Cedar, Pine, Spruce.......( 1,10Σ4 PSI )"
320 PRINT "  < h >  OTHER............................"
330 PRINT "    or"
340 PRINT "  < z >  to EXIT"
350 PRINT UL$;
360 Z$=INKEY$:IF Z$=""THEN 360
370 VIEW PRINT 13 TO 24:CLS:VIEW PRINT:LOCATE 13
380 :REM'........PCI = pounds per cubic inch
390 IF Z$="a"THEN E=30000000.0!:PCI=0.2833000123500824!:BM$="Steel Alloy":GOTO 510
400 IF Z$="b"THEN E=29000000.0!:PCI=0.2833000123500824!:BM$="Structural Steel":GOTO 510
410 IF Z$="c"THEN E=28000000.0!:PCI=0.2849999964237213!:BM$="Wrought Iron":GOTO 510
420 IF Z$="d"THEN E=10300000.0!:PCI=0.09790001064538956!:BM$="Extruded Aluminum":GOTO 510
430 IF Z$="e"THEN E=1760000.0!:PCI=0.016200000420212746!:BM$="Fir":GOTO 510
440 IF Z$="f"THEN E=1320000.0!:PCI=0.016200000420212746!:BM$="Redwood":GOTO 510
450 IF Z$="g"THEN E=1100000.0!:PCI=0.016200000420212746!:BM$="Cedar, Pine or Spruce":GOTO 510
460 IF Z$="h"THEN BM$="Unspecified":GOTO 490
470 IF Z$="z"THEN CLS:CHAIN "mechmenu"
480 GOTO 360
490 INPUT " ENTER: Modulus of elasticity ";E
500 LOCATE CSRLIN-1:PRINT STRING$(80,32);:LOCATE CSRLIN-1
510 PRINT UL$;
520 PRINT " Modulus of Elasticity = ";USING "###,###,###";E;
530 PRINT " PSI for ";BM$
540 PRINT UL$;
550 PRINT " Press < 1 > to continue or < 0 > to re-do....."
560 Z$=INKEY$
570 IF Z$="1"THEN 610
580 IF Z$="0"THEN 100
590 GOTO 560
600 :REM'
610 :REM'.....define beam
620 CLS:COLOR 7,0,0
630 PRINT " Press letter in ( ) to define beam:"
640 PRINT UL$;
650 PRINT" (a) case  1: Supported both ends, uniform load"
660 PRINT" (b) case  2: Supported both ends, load at centre"
670 PRINT" (c) case  3: Supported both ends, load at any point"
680 PRINT" (d) case  4: Supported both ends, two symmetrical loads"
690 PRINT
700 PRINT" (k) case 10: Fixed one end, uniform load (cantilever)"
710 PRINT" (l) case 11: Fixed one end, load at other (cantilever)"
720 PRINT" (m) case 12: Fixed one end, intermediate load (cantilever)"
730 PRINT
740 PRINT" (r) case 18: Fixed both ends, uniform load"
750 PRINT" (s) case 19: Fixed both ends, load at centre"
760 PRINT" (t) case 20: Fixed both ends, load at any point"
770 PRINT UL$;
780 Z$=INKEY$
790 IF Z$="a"OR Z$="A"THEN 1190
800 IF Z$="b"OR Z$="B"THEN 1460
810 IF Z$="c"OR Z$="C"THEN 1720
820 IF Z$="d"OR Z$="D"THEN 2010
830 :REM'
840 IF Z$="k"OR Z$="K"THEN 2290
850 IF Z$="l"OR Z$="L"THEN 2550
860 IF Z$="m"OR Z$="M"THEN 2810
870 :REM'
880 IF Z$="r"OR Z$="R"THEN 3090
890 IF Z$="s"OR Z$="S"THEN 3350
900 IF Z$="t"OR Z$="T"THEN 3610
910 GOTO 780
920 :REM'
930 :REM'INPUTS
940 :REM'
950 :REM'.....input load
960 IF W=0 THEN INPUT "ENTER: Load (in pounds).....................W = ";W
970 RETURN
980 :REM'
990 :REM'.....input moment of inertia
1000 IF I=0 THEN INPUT "ENTER: Moment of Inertia (in inches^4)......I = ";I
1010 RETURN
1020 :REM'
1030 :REM'.....input length
1040 IF L=0 THEN INPUT "ENTER: Length of beam (in inches)...........L = ";L
1050 RETURN
1060 :REM'
1070 :REM'.....input dimension A
1080 IF A=0 THEN INPUT "ENTER: Load distance from nearest end.......A = ";A
1090 RETURN
1100 :REM'
1110 :REM'.....input dimension A
1120 IF AA=0 THEN INPUT "ENTER: Load distance from fixed end........AA = ";AA
1130 RETURN
1140 :REM'
1150 :REM'.....input deflection
1160 IF D=0 THEN INPUT "ENTER: Deflection (in inches)...............D = ";D
1170 RETURN
1180 :REM'
1190 :REM'.....case 1
1200 CLS
1210 PRINT USING " Case ##:";ASC(Z$)-96;
1220 PRINT " BEAM SUPPORTED BOTH ENDS, UNIFORM LOAD"
1230 PRINT " ";BM$;X$
1240 PRINT UL$;
1250 D$="centre":D1$=""
1260 TB=30
1270 PRINT
1280 PRINT TAB(TB)"D= (5/384) x (W x L^3) / (E x I)"
1290 LOCATE 4,1:PRINT " █████████████████ W"
1300 LOCATE 5,1:PRINT " ╪═══╧═══╧═══╧═══╪"
1310 LOCATE 6,1:PRINT " ^               ^"
1320 PRINT UL$;
1330 LIN=CSRLIN
1340 GOSUB 950:GOSUB 1390
1350 GOSUB 990:GOSUB 1390
1360 GOSUB 1030:GOSUB 1390
1370 GOSUB 1150:GOSUB 1390
1380 GOTO 1340
1390 :REM'.....calculate
1400 IF W*L*E*I THEN D=5*W*L^3/(384*E*I):GOTO 3900
1410 IF D*E*I*L THEN W=D*384*E*I/(5*L^3):GOTO 3900
1420 IF D*W*E*L THEN I=W*5*L^3/(D*384*E):GOTO 3900
1430 IF D*W*E*I THEN L=(D*384*E*I/(5*W))^(1/3):GOTO 3900
1440 RETURN
1450 :REM'
1460 :REM'.....case 2
1470 CLS
1480 PRINT " BEAM SUPPORTED BOTH ENDS, LOAD AT CENTRE"
1490 PRINT X$
1500 D$="load":D1$=""
1510 PRINT UL$;
1520 TB=30
1530 PRINT
1540 PRINT TAB(TB)"D= (W x L^3) / (48 x E x I)"
1550 LOCATE 4,1:PRINT "         █ W"
1560 LOCATE 5,1:PRINT " ╤═══════╧═══════╤"
1570 LOCATE 6,1:PRINT " ^               ^"
1580 PRINT UL$;
1590 LIN=CSRLIN
1600 GOSUB 950:GOSUB 1650
1610 GOSUB 990:GOSUB 1650
1620 GOSUB 1030:GOSUB 1650
1630 GOSUB 1150:GOSUB 1650
1640 GOTO 1600
1650 :REM'.....calculate
1660 IF W*L*E*I THEN D=W*L^3/(48*E*I):GOTO 3900
1670 IF D*E*I*L THEN W=D*48*E*I/L^3:GOTO 3900
1680 IF D*E*I*W THEN L=(D*48*E*I/W)^(1/3):GOTO 3900
1690 IF W*L*D*E THEN I=W*L^3/(D*48*E):GOTO 3900
1700 RETURN
1710 :REM'
1720 :REM'.....case 3
1730 CLS
1740 PRINT " BEAM SUPPORTED BOTH ENDS, LOAD AT ANY POINT"
1750 PRINT X$
1760 D$="load":D1$=""
1770 PRINT UL$;
1780 TB=30
1790 PRINT
1800 PRINT TAB(TB)"D= (W x A^2 x B^2) / (3 x E x I x L)"
1810 LOCATE 4,1:PRINT "            █ W"
1820 LOCATE 5,1:PRINT " ╤══════════╧════╤"
1830 LOCATE 6,1:PRINT " ^               ^"
1840 PRINT UL$;
1850 LIN=CSRLIN
1860 GOSUB 950:GOSUB 1920
1870 GOSUB 990:GOSUB 1920
1880 GOSUB 1030:GOSUB 1920
1890 GOSUB 1070:GOSUB 1920
1900 GOSUB 1150:GOSUB 1920
1910 GOTO 1860
1920 :REM'.....calculate
1930 IF A*L THEN B=L-A
1940 IF W*A*B*E*I*L THEN D=W*A^2*B^2/(3*E*I*L):GOTO 3900
1950 IF D*E*I*L*A*B THEN W=D*3*E*I*L/(A^2*B^2):GOTO 3900
1960 IF D*E*I*L*W*B THEN A=SQR(D*3*E*I*L/(W*B^2)):GOTO 3900
1970 IF W*A*B*D*E*L THEN I=W*A^2*B^2/(D*3*E*L):GOTO 3900
1980 IF W*A*B*D*E*I THEN L=W*A^2*B^2/(D*3*E*I):GOTO 3900
1990 RETURN
2000 :REM'
2010 :REM'.....case 4
2020 CLS
2030 PRINT " BEAM SUPPORTED BOTH ENDS, TWO SYMMETRICAL LOADS"
2040 PRINT X$
2050 D$="centre":D1$="loads"
2060 PRINT UL$;
2070 TB=20
2080 PRINT
2090 PRINT TAB(TB)"at centre: D= (W x A) / (24 x E x I) x (3 x L^2 - 4 x A^2)"
2100 PRINT TAB(TB)"at loads: D1= (W x A^2) / (6 x E x I) x (3 x L - 4 x A)"
2110 LOCATE 4,1:PRINT "     █ W     █ W"
2120 LOCATE 5,1:PRINT " ╤═══╧═══════╧═══╤"
2130 LOCATE 6,1:PRINT " ^               ^"
2140 PRINT UL$;
2150 LIN=CSRLIN
2160 GOSUB 950:GOSUB 2220
2170 GOSUB 990:GOSUB 2220
2180 GOSUB 1030:GOSUB 2220
2190 GOSUB 1070:GOSUB 2220
2200 GOSUB 1150:GOSUB 2220
2210 GOTO 2160
2220 :REM'.....calculate
2230 IF W*A*L*E*I THEN 2240 :ELSE 2250
2240 D=W*A*(3*L^2-4*A^2)/(24*E*I):D1=W*A^2*(3*L-4*A)/(6*E*I):GOTO 3900
2250 IF D*A*L*E*I THEN W=D/(A*(3*L^2-4*A^2)/(24*E*I)):GOTO 3900
2260 IF D1*A*L*E*I THEN W=D1/(A^2*(3*L-4*A)/(6*E*I)):GOTO 3900
2270 RETURN
2280 :REM'
2290 :REM'.....case 10
2300 CLS
2310 PRINT " CANTILEVER BEAM FIXED ONE END, UNIFORM LOAD"
2320 PRINT X$
2330 D$="end":D1$=""
2340 PRINT UL$;
2350 TB=30
2360 PRINT
2370 PRINT TAB(TB)"D= (W x L^3) / (8 x E x I)"
2380 LOCATE 4,1:PRINT "  ████████████████ W"
2390 LOCATE 5,1:PRINT " ╬╧══╧══╧══╧══╧══╧"
2400 LOCATE 6,1:PRINT " ║"
2410 PRINT UL$;
2420 LIN=CSRLIN
2430 GOSUB 950:GOSUB 2480
2440 GOSUB 990:GOSUB 2480
2450 GOSUB 1030:GOSUB 2480
2460 GOSUB 1150:GOSUB 2480
2470 GOTO 2430
2480 :REM'.....calculate
2490 IF W*L*E*I THEN D=W*L^3/(8*E*I):GOTO 3900
2500 IF D*E*I*L THEN W=D*8*E*I/L^3:GOTO 3900
2510 IF D*E*I*W THEN L=(D*8*E*I/W)^(1/3):GOTO 3900
2520 IF W*L*D*E THEN I=W*L^3/(D*8*E):GOTO 3900
2530 RETURN
2540 :REM'
2550 :REM'.....case 11
2560 CLS
2570 PRINT " CANTILEVER BEAM FIXED ONE END, LOAD AT OTHER"
2580 PRINT " ";BM$;X$
2590 D$="end":D1$=""
2600 PRINT UL$;
2610 TB=30
2620 PRINT
2630 PRINT TAB(TB)"D= (W x L^3) / (3 x E x I)"
2640 LOCATE 4,1:PRINT "                  █ W"
2650 LOCATE 5,1:PRINT " ╬════════════════╛"
2660 LOCATE 6,1:PRINT " ║"
2670 PRINT UL$;
2680 LIN=CSRLIN
2690 GOSUB 950:GOSUB 2740
2700 GOSUB 990:GOSUB 2740
2710 GOSUB 1030:GOSUB 2740
2720 GOSUB 1150:GOSUB 2740
2730 GOTO 2690
2740 :REM'.....calculate
2750 IF W*L*E*I THEN D=W*L^3/(3*E*I):GOTO 3900
2760 IF D*E*I*L THEN W=D*3*E*I/L^3:GOTO 3900
2770 IF D*E*I*W THEN L=(D*3*E*I/W)^(1/3):GOTO 3900
2780 IF W*L*D*E THEN I=W*L^3/(D*3*E):GOTO 3900
2790 RETURN
2800 :REM'
2810 :REM'.....case 12
2820 CLS
2830 PRINT " ";BM$;X$
2840 PRINT " BEAM FIXED AT ONE END, INTERMEDIATE LOAD"
2850 D$="load":D1$="end"
2860 PRINT UL$;
2870 TB=20
2880 PRINT
2890 PRINT TAB(TB)"at load: D= (W x AA^3) / (3 x E x I)"
2900 PRINT TAB(TB)"at end: D1= (W x AA^2) / (6 x E x I) x (2 x AA + 3 x BB)"
2910 LOCATE 4,1:PRINT "           █ W"
2920 LOCATE 5,1:PRINT " ╬═════════╧════"
2930 LOCATE 6,1:PRINT " ║"
2940 PRINT UL$;
2950 LIN=CSRLIN
2960 GOSUB 950:GOSUB 3010
2970 GOSUB 990:GOSUB 3010
2980 GOSUB 1030:GOSUB 3010
2990 GOSUB 1110:GOSUB 3010
3000 GOTO 2960
3010 :REM'.....calculate
3020 IF BB=0 THEN IF L*AA THEN BB=L-AA
3030 IF W*E*I*AA*BB=0 THEN 3070
3040 D=(W*AA^3)/(3*E*I)
3050 D1=(W*AA^2)/(6*E*I)*(2*AA+6*BB)
3060 GOTO 3900
3070 RETURN
3080 :REM'
3090 :REM'.....case 18
3100 CLS
3110 PRINT " BEAM FIXED BOTH ENDS, UNIFORM LOAD"
3120 PRINT " ";BM$;X$
3130 D$="centre":D1$=""
3140 PRINT UL$;
3150 TB=30
3160 PRINT
3170 PRINT TAB(TB)"D= (W x L^3) / (384 x E x I)"
3180 LOCATE 4,1:PRINT "  █████████████████ W"
3190 LOCATE 5,1:PRINT " ╬╧═══╧═══╧═══╧═══╧╬"
3200 LOCATE 6,1:PRINT " ║                 ║"
3210 PRINT UL$;
3220 LIN=CSRLIN
3230 GOSUB 950:GOSUB 3280
3240 GOSUB 990:GOSUB 3280
3250 GOSUB 1030:GOSUB 3280
3260 GOSUB 1150:GOSUB 3280
3270 GOTO 3230
3280 :REM'.....CALCULATE
3290 IF W*L*E*I THEN D=W*L^3/(384*E*I):GOTO 3900
3300 IF D*E*I*L THEN W=D*384*E*I/L^3:GOTO 3900
3310 IF D*E*I*W THEN L=(D*384*E*I/W)^(1/3):GOTO 3900
3320 IF W*L*D*E THEN I=W*L^3/(D*384*E):GOTO 3900
3330 RETURN
3340 :REM'
3350 :REM'.....case 19
3360 CLS
3370 PRINT " BEAM FIXED BOTH ENDS, LOAD AT CENTRE"
3380 D$="load":D1$=""
3390 PRINT " ";BM$;X$
3400 PRINT UL$;
3410 TB=30
3420 PRINT
3430 PRINT TAB(TB)"D= (W x L^3) / (192 x E x I)"
3440 LOCATE 4,1:PRINT "          █ W"
3450 LOCATE 5,1:PRINT " ╬════════╧════════╬"
3460 LOCATE 6,1:PRINT " ║                 ║"
3470 PRINT UL$;
3480 LIN=CSRLIN
3490 GOSUB 950:GOSUB 3540
3500 GOSUB 990:GOSUB 3540
3510 GOSUB 1030:GOSUB 3540
3520 GOSUB 1150:GOSUB 3540
3530 GOTO 3490
3540 :REM'.....CALCULATE
3550 IF W*L*E*I THEN D=W*L^3/(192*E*I):GOTO 3900
3560 IF D*E*I*L THEN W=D*192*E*I/L^3:GOTO 3900
3570 IF D*E*I*W THEN L=(D*192*E*I/W)^(1/3):GOTO 3900
3580 IF W*L*D*E THEN I=W*L^3/(D*192*E):GOTO 3900
3590 RETURN
3600 :REM'
3610 :REM'.....case 20
3620 CLS
3630 PRINT " BEAM FIXED BOTH ENDS, LOAD AT ANY POINT"
3640 PRINT " ";BM$;X$
3650 D$="load":D1$=""
3660 PRINT UL$;
3670 TB=30
3680 PRINT
3690 PRINT TAB(TB)"D= (W x A^3 x B^3)) / (3 x E x I x L^3)"
3700 LOCATE 4,1:PRINT "             █ W"
3710 LOCATE 5,1:PRINT " ╬═══════════╧════╬"
3720 LOCATE 6,1:PRINT " ║                ║"
3730 PRINT UL$;
3740 LIN=CSRLIN
3750 GOSUB 950:GOSUB 3810
3760 GOSUB 990:GOSUB 3810
3770 GOSUB 1030:GOSUB 3810
3780 GOSUB 1070:GOSUB 3810
3790 GOSUB 1150:GOSUB 3810
3800 GOTO 3750
3810 :REM'.....calculate
3820 IF L*A THEN B=L-A
3830 IF W*A*B*E*I*L THEN D=W*A^3*B^3/(3*E*I*L^3):GOTO 3900
3840 IF D*E*I*L*A*B THEN W=D*3*E*I*L^3/(A^3*B^3):GOTO 3900
3850 IF D*E*I*L*W*B THEN A=(D*3*E*I*L^3/(W*B^3))^(1/3):GOTO 3900
3860 IF W*A*B*D*E*L THEN I=W*A^3*B^3/(D*3*E*L^3):GOTO 3900
3870 IF W*A*B*D*E*I THEN L=(W*A^3*B^3/(D*3*E*I))^(1/3):GOTO 3900
3880 RETURN
3890 :REM'
3900 :REM'.....screen printout
3910 IF LN=0 THEN 4050
3920 :REM'
3930 :REM'.....print snapshot
3940 VIEW PRINT LIN TO 24:CLS:VIEW PRINT:LOCATE LIN
3950 OPEN"I",1,"\hamcalc\data\snapshot.fil"
3960 LIN=0
3970 IF EOF(1)THEN 4010
3980 INPUT#1,LN$
3990 PRINT LN$;
4000 GOTO 3970
4010 PRINT ""
4020 CLOSE
4030 LIN=CSRLIN-1
4040 :REM'
4050 VIEW PRINT LIN TO 24:CLS:VIEW PRINT:LOCATE LIN
4060 U$="########,###.###"
4070 V$="########,###.#"
4080 O$=" in."
4090 PRINT TAB(8);" Modulus of Elasticity .....................E =";USING V$;E;
4100 PRINT "    PSI"
4110 IF LN*I THEN 4140
4120 PRINT TAB(8);" Moment of Inertia .........................I =";USING U$;I;
4130 PRINT " in^4"
4140 PRINT TAB(8);" Length of beam ............................L =";USING U$;L;
4150 PRINT O$
4160 PRINT TAB(8);" Load ......................................W =";USING V$;W;
4170 PRINT "    lbs."
4180 IF PCI*AR=0 THEN 4220
4190 X=AR*L*PCI
4200 PRINT TAB(8);" Approximate weight of beam.................. =";USING V$;X;
4210 PRINT "    lbs."
4220 IF A=0 THEN 4280
4230 PRINT TAB(8);" Distance - load to nearest end of beam.....A =";USING U$;A;
4240 PRINT O$
4250 IF B=0 THEN 4280
4260 PRINT TAB(8);" Distance - load to farthest end of beam....B =";USING U$;B;
4270 PRINT O$
4280 IF AA*BB=0 THEN 4330
4290 PRINT TAB(8);" Distance - load to fixed end of beam......AA =";USING U$;AA;
4300 PRINT O$
4310 PRINT TAB(8);" Distance - load to free end of beam.......BB =";USING U$;BB;
4320 PRINT O$
4330 PRINT TAB(8);" Deflection at .............................D =";USING U$;D;
4340 PRINT O$
4350 LOCATE CSRLIN-1,23:PRINT D$
4360 IF D1=0 THEN 4400
4370 PRINT TAB(8);" Deflection at ............................D1 =";USING U$;D1;
4380 PRINT O$
4390 LOCATE CSRLIN-1,23:PRINT D1$
4400 MD=L/360
4410 IF D>D1 THEN LIMIT=D :ELSE LIMIT=D1
4420 IF LIMIT>MD THEN BEEP:COLOR 12 :ELSE COLOR 7
4430 PRINT TAB(8);" Maximum safe deflection ......................";USING U$;MD;
4440 PRINT O$;
4450 COLOR 7
4460 GOSUB 4490
4470 CLS:CHAIN"mechmenu"
4480 :REM'
4490 :REM'PRT
4500 KEY OFF:GOSUB 4570:LOCATE 25,5:COLOR 0,2
4510 PRINT " Send this page to:(1)Printer Queue? (2)Printout? ";
4520 PRINT "(3)Next page? (1/2/3)";:COLOR 7,0
4530 Z$=INKEY$:IF Z$<"1"OR Z$>"3"THEN 4530 :ELSE GOSUB 4570
4540 IF Z$="3"THEN RETURN
4550 FOR I%=1 TO 24:FOR J%=1 TO 80:LPRINT CHR$(SCREEN(I%,J%));:NEXT J%:NEXT I%
4560 IF Z$="2"THEN LPRINT CHR$(12) :ELSE 4500
4570 LOCATE 25,1:PRINT STRING$(80,32);:RETURN
