10 :REM'HARMONIC - Harmonic Frequencies - 25 JUL 95 rev.05 OCT 97
20 IF EX$=""THEN EX$="EXIT"
30 COMMON EX$
40 CLS:KEY OFF
50 COLOR 7,0,1
60 UL$=STRING$(80,205)
70 U$="######.###"
80 DIM F$(20,3)
90 DATA  160m.,1.800,2.000,   80m.,3.500,4.000,    40m.,7.000,7.300
100 DATA  30m.,10.100,10.150,  20m.,14.000,14.350,  17m.,18.086,18.168
110 DATA  15m.,21.000,21.450,  12m.,24.890,24.990,  10m.,28.000,29.700
120 DATA  6m.,50.000,54.000,   2m.,144.000,148.000, 1.25m.,220.000,225.000
130 DATA  70cm.,420.000,450.000, 33cm.,902.000,928.000, 23cm.,1240,1300
140  FOR Z=1 TO 15:FOR Y=1 TO 3
150  READ F$(Z,Y)
160  NEXT Y:NEXT Z
170 :REM'
180 COLOR 15,2
190 PRINT " HARMONIC FREQUENCIES";TAB(57);"by George Murphy VE3ERP ";
200 COLOR 1,0:PRINT STRING$(80,223);
210 COLOR 7,0
220 :REM'
230 :REM'.....start
240 VIEW PRINT 3 TO 24:CLS:VIEW PRINT:LOCATE 3
250 :REM'.....menu
260 PRINT " Press number in < > to:"
270 PRINT UL$;
280 PRINT "   < 1 >  Find HARMONIC FREQUENCIES"
290 PRINT "   < 2 >  Find FUNDAMENTAL FREQUENCY"
300 PRINT "   < 3 >  Find T.V. CHANNEL FREQUENCIES"
310 PRINT UL$;
320 PRINT "   < 0 >  EXIT"
330 Z$=INKEY$:IF Z$=""THEN 330
340 IF Z$="0"THEN CLS:RUN EX$
350 IF Z$="1"THEN 400
360 IF Z$="2"THEN 760
370 IF Z$="3"THEN CLS:CHAIN"tvchan"
380 GOTO 330
390 :REM'
400 :REM'.....harmonics
410 VIEW PRINT 3 TO 24:CLS:VIEW PRINT:LOCATE 3
420 INPUT " ENTER: Fundamental frequency (MHz) ";FF
430 :REM'
440 VIEW PRINT 3 TO 24:CLS:VIEW PRINT:LOCATE 3
450 PRINT TAB(25);"FREQUENCY";TAB(36);"WAVELENGTH";TAB(50);"HAM BAND ?"
460 PRINT UL$;
470 FOR Z=1 TO 20
480 IF Z=1 THEN B$=" FUNDAMENTAL FQCY.:":FQ=FF:GOTO 560
490 A$=STR$(Z):IF LEN(A$)<3 THEN A$=" "+A$
500 B$=A$
510 IF Z=2 THEN B$=B$+"nd"
520 IF Z=3 THEN B$=B$+"rd"
530 IF Z>=4 THEN B$=B$+"th"
540 B$=B$+" harmonic....."
550 FQ=FF*Z
560 PRINT B$;USING U$;FQ;
570 PRINT " MHz";USING U$;300/FQ;
580 PRINT "m.";
590 C$=F$(Z,1)
600 D$=""
610   FOR Y=1 TO 15
620    V1=VAL(F$(Y,2))
630    V2=VAL(F$(Y,3))
640    IF FQ<V1 OR FQ>V2 THEN 680
650    C$=F$(Y,1)
660    D$="("+F$(Y,2)+" - "+F$(Y,3)+" MHz)"
670    GOTO 700
680   NEXT Y
690 C$=" - no -"
700 PRINT TAB(50);C$;
710 IF D$<>"" THEN PRINT TAB(56);D$;
720 IF Z<20 THEN PRINT ""
730 NEXT Z
740 GOSUB 1060:GOTO 230
750 :REM'
760 :REM'.....fundamental frequency
770 VIEW PRINT 3 TO 24:CLS:VIEW PRINT:LOCATE 3
780 INPUT "ENTER: Frequency (MHz) of harmonic";FH
790 FQ=FH
800 VIEW PRINT 3 TO 24:CLS:VIEW PRINT:LOCATE 3
810 PRINT " Harmonic frequency:";USING U$;FH;:PRINT " MHz is the:"
820 :REM'
830 K$=" MHz":M=1
840 H=1
850 :REM'
860 H=H+1:FQ=FH/H
870 IF H>22 THEN 1030
880 H$=STR$(H)
890 IF LEN(H$)<3 THEN H$=" "+H$
900 H$=RIGHT$(H$,2)
910 N$="th"
920 IF H=2 THEN N$="nd"
930 IF H=3 THEN N$="rd"
940 IF FQ<1 THEN M=10^3:K$=" kHz"
950 LOCATE H+1,42:PRINT H$+N$+" harmonic of";USING U$;FQ*M;:PRINT K$
960 IF FQ<0.8999999761581421! THEN 980
970 GOTO 860
980 LOCATE CSRLIN-1,72:COLOR 0,7:PRINT " * "
990 PRINT
1000 LOCATE CSRLIN,2
1010 PRINT " * ";:COLOR 7,0:PRINT " Audio frequency. ";
1020 PRINT "Radio Frequencies start at about 9 kHz."
1030 GOSUB 1060:GOTO 230
1040 END
1050 :REM'
1060 :REM'PRT
1070 KEY OFF:GOSUB 1140:LOCATE 25,5:COLOR 0,2
1080 PRINT " Send this page to:(1)Printer Queue? (2)Printout? ";
1090 PRINT "(3)Next page? (1/2/3)";:COLOR 7,0
1100 Z$=INKEY$:IF Z$<"1"OR Z$>"3"THEN 1100 :ELSE GOSUB 1140
1110 IF Z$="3"THEN RETURN
1120 FOR I%=1 TO 24:FOR J%=1 TO 80:LPRINT CHR$(SCREEN(I%,J%));:NEXT J%:NEXT I%
1130 IF Z$="2"THEN LPRINT CHR$(12) :ELSE 1070
1140 LOCATE 25,1:PRINT STRING$(80,32);:RETURN
