10 :REM'COAXSTUB - Stubs for Coaxial Lines - 03 JUN 96 rev. 07 AUG 2001
20 CLS:KEY OFF
30 IF EX$=""THEN EX$="EXIT"
40 IF PROG$=""THEN GO$=EX$ :ELSE GO$=PROG$
50 COMMON EX$
60 COLOR 7,0,1
70 ON ERROR  GOTO 100
80 GOTO 130
90 :REM'
100 IF ERR=1 AND ERL=1520 THEN COLOR 7,0:RESUME 1840
110 BEEP:PRINT "Error";ERR;"in line";ERL:END
120 :REM'
130 PI=3.1415929794311523!
140 UL$=STRING$(80,205)
150 L$="#####.###"
160 V$="####.###"
170 X$=STRING$(80,32)
180 DIM C$(51,9)    :REM'coax data
190 :REM'
200 :REM'.....read data
210 OPEN"I",1,"\hamcalc\coaxfile\coaxial.fil"
220 N=47    :REM'no.of data groups
230  FOR Z=1 TO N
240   FOR Y=1 TO 9
250   INPUT#1, C$(Z,Y)
260   NEXT Y
270  NEXT Z
280 CLOSE
290 :REM'
300 :REM'.....start
310 CLS
320 B=0:C=0:E=0:I=0:O=0:U=0:V=0:ZO=0
330 COLOR 15,2
340 PRINT " STUBS for COAXIAL TRANSMISSION LINES";
350 PRINT TAB(57)"by George Murphy VE3ERP ";
360 COLOR 1,0:PRINT STRING$(80,223);
370 COLOR 7,0
380 GOSUB 2340
390 PRINT UL$;
400 PRINT " Press number in < > to choose standard units of measure:"
410 PRINT UL$;
420 PRINT "   < 1 >  Metric"
430 PRINT "   < 2 >  U.S.A./Imperial"
440 PRINT UL$;
450 PRINT "     or Press < 0 > to EXIT....."
460 Z$=INKEY$
470 IF Z$="0"THEN CLS:CHAIN GO$
480 IF Z$="1"THEN UM=0.30480000376701355!:UM$="m.":GOTO 520
490 IF Z$="2"THEN UM=1:UM$="ft.":GOTO 520
500 GOTO 460
510 :REM'
520 :REM'.....transmission line stubs
530 CLS
540 LOCATE 1:COLOR 0,7:T=1
550 LOCATE ,T:PRINT "           COAXIAL TRANSMISSION LINE STUBS           "
560 LOCATE ,T:PRINT "      │«── Lg < W/4 ──»│        │«── Lg < W/4 ──»│   "
570 LOCATE ,T:PRINT "      ╒════════════════╕        ╒════════════════╕   "
580 LOCATE ,T:PRINT " ┌──» ──────────────────   ┌──» ──────────────────┐  "
590 LOCATE ,T:PRINT " Z in ╘╤════shield═════╛   Z in ╘╤════shield════╤╛│  "
600 LOCATE ,T:PRINT " └──» ─┘                   └──» ─┘              └─┘  "
610 COLOR 7,0
620 LOCATE ,T:PRINT "█ INDUCTIVE (open) STUB █ CAPACITIVE (shorted) STUB █"
630 COLOR 0,7
640 LOCATE ,T:PRINT "                                                     "
650 LOCATE ,T:PRINT "     (ref. 1996 ARRL HANDBOOK, pp.16.21 & 16.22)     "
660 COLOR 7,0
670 T=55:LOCATE 3
680 LOCATE ,T:PRINT "Transmission line stubs  "
690 LOCATE ,T:PRINT "should extend away from  "
700 LOCATE ,T:PRINT "the line at an angle as  "
710 LOCATE ,T:PRINT "close to 90° as possible."
720 LOCATE 10:PRINT UL$;
730 GOSUB 760
740 GOTO 880
750 :REM'
760 :REM'.....cable table
770 :REM'FOR Z=1 TO 8
780 :REM'FOR Y=0 TO 36 STEP 8
790 :REM'Z$=STR$(Z+Y):IF LEN(Z$)<3 THEN Z$=" "+Z$
800 :REM'Y$=Z$+":"+C$(Z+Y,1)
810 :REM'IF LEN(Y$)<16 THEN Y$=Y$+" ":GOTO 810
820 :REM'IF Z<10 AND Y=0 THEN Y$=LEFT$(Y$,15)
830 :REM'PRINT Y$;
840 :REM'NEXT Y
850 :REM'NEXT Z
851 FOR Z=1 TO 10
852 PRINT TAB(1)USING "##";Z;:PRINT C$(Z,1);
853 PRINT TAB(21)USING "##";Z+10;:PRINT C$(Z+10,1);
854 PRINT TAB(41)USING "##";Z+20;:PRINT C$(Z+20,1);
855 PRINT TAB(61)USING "##";Z+40;:PRINT C$(Z+30,1)
856 NEXT Z
860 PRINT TAB(27)"* denotes foam dielectric"
870 RETURN
880 PRINT UL$;
890 :REM'
900 INPUT " ENTER: An item number from above list to select coaxial cable";N
910 IF N>=1 AND N<=40 THEN 930
920 BEEP:LOCATE CSRLIN-1:PRINT X$;:LOCATE CSRLIN-1:GOTO 900
930 VIEW PRINT 11 TO 24:CLS:VIEW PRINT
940 FOR Z=1 TO 9:LOCATE Z,55:PRINT STRING$(26,32);:NEXT Z
950 T$=C$(N,1)
960 ZIN=VAL(C$(N,5))
970 E=VAL(C$(N,3))
980 VF=1/SQR(E)
990 COLOR 15,4
1000 LOCATE 1,54:PRINT " ";T$;" COAX CABLE "
1010 COLOR 7,0
1020 LOCATE ,55:PRINT "Impedance Z (ohms) =";USING"###.#";ZIN
1030 LOCATE ,55:PRINT "Inner wire dia.,in.=";USING" .###";VAL(C$(N,8))
1040 LOCATE ,55:PRINT "Dielectric constant=";USING"##.##";E
1050 LOCATE ,55:PRINT "Velocity factor....=";USING" .###";VF
1060 LOCATE ,55:PRINT "XL= Inductive Reactance"
1070 LOCATE ,55:PRINT " L= Inductance"
1080 LOCATE ,55:PRINT "XC= Capacitive Reactance"
1090 LOCATE ,55:PRINT " C= Capacitance"
1100 LOCATE 11
1110 :REM'
1120 INPUT " ENTER: Frequency in MHz......";FO
1130 COLOR 7,0
1140 VIEW PRINT 23 TO 24:CLS:VIEW PRINT:LOCATE 11
1150 WL=300/FO          :REM'wavelength in free space
1160 W=300/(SQR(E)*FO)  :REM'electrical wavelength
1170 :REM'
1180 T=9
1190 PRINT TAB(T)"Frequency.......................";USING V$;FO;
1200 PRINT " MHz    =";
1210 PRINT USING V$;300/FO;:PRINT " metres"
1220 PRINT TAB(T)"Electrical wavelength.........w=";USING V$;W;
1230 PRINT " metres =";USING V$;W/0.30480000376701355!;:PRINT " feet"
1240 PRINT TAB(T)"Electrical ¼-wavelength.....w/4=";USING V$;W/4;
1250 PRINT " metres =";USING V$;W/4/0.30480000376701355!;:PRINT " feet"
1260 PRINT UL$;
1270 PRINT " TYPICAL VALUES:";TAB(38)"INDUCTIVE STUB";
1280 PRINT TAB(58)"CAPACITIVE STUB"
1290 PRINT TAB(9)"Lg/w";TAB(16)"Lg(m.)";TAB(25)"Lg(ft)";
1300 PRINT TAB(38);"XL(Ω)";TAB(48)"L(µH)";TAB(58)"XC(Ω)";TAB(68)"C(pF)"
1310 :REM'
1320 K=-0.05000000074505806!
1330 FOR Z=1 TO 7
1340 IF Z=4 OR Z=5 THEN K=K+0.02500000037252903!:GOTO 1360
1350 K=K+0.05000000074505806!
1360 LG=K*W
1370 :REM'
1380 IF K=0 THEN XL=0:XC=10^6:GOTO 1430
1390 IF K=0.25! THEN XC=0:XL=10^6:GOTO 1430
1400 XC=ZIN/TAN(2*PI*LG/W):      C=10^6/(2*PI*FO*XC)
1410 XL=ZIN/(1/TAN(2*PI*LG/W)):  L=XL/(2*PI*FO)
1420 :REM'
1430 PRINT TAB(9)USING ".###";K;
1440 PRINT TAB(13);USING L$;LG;
1450 PRINT TAB(22);USING L$;LG/0.30480000376701355!;
1460 IF K<=0   THEN PRINT TAB(38)"- 0 -     - 0 -     - ∞ -     - 0 -":GOTO 1520
1470 IF K>=0.25! THEN PRINT TAB(38)"- ∞ -     - ∞ -     - 0 -     - ∞ -":GOTO 1520
1480 PRINT TAB(34)USING L$;XL;
1490 PRINT TAB(44)USING L$;L;
1500 PRINT TAB(54)USING L$;XC;
1510 PRINT TAB(64)USING L$;C;
1520 NEXT Z
1530 GOSUB 2570   :REM'hardcopy
1540 :REM'
1550 :REM'.....sub-menu
1560 COLOR 7,0
1570 LOCATE 25,1:PRINT X$;
1580 LOCATE 24,3:COLOR 15,2
1590 PRINT " Press a number for a specific: 1-Frequency, 2-Length, ";
1600 PRINT "3-Inductance,        ";
1610 LOCATE 25,3
1620 PRINT " 4-Capacitance, 5-Inductive Reactance, 6-Capacitive Reactance,";
1630 PRINT " or 7 to QUIT ";
1640 :REM'
1650 COLOR 7,0
1660 Z$=INKEY$:IF Z$=""THEN 1660
1670 IF VAL(Z$)<1 OR VAL(Z$)>7 THEN 1660
1680 LOCATE 25,1:PRINT X$;
1690 IF Z$="1"THEN 1860
1700 IF Z$="2"THEN 1770
1710 IF Z$="3"THEN 1900
1720 IF Z$="4"THEN 2110
1730 IF Z$="5"THEN 2010
1740 IF Z$="6"THEN 2230
1750 IF Z$="7"THEN 2540
1760 :REM'
1770 :REM'.....length
1780 COLOR 7,0:VIEW PRINT 23 TO 24:CLS:VIEW PRINT:LOCATE 23
1790 COLOR 0,7:PRINT " ENTER: Specified length....(";UM$;")";:INPUT LG
1800 IF UM=1 THEN LG=LG*0.30480000376701355!
1810 K=LG/W
1820 LOCATE CSRLIN-1:PRINT X$;
1830 LOCATE CSRLIN-1,8:GOTO 1380
1840 GOSUB 2570:GOTO 1550
1850 :REM'
1860 :REM'.....frequency
1870 COLOR 7,0:VIEW PRINT 23 TO 24:CLS:VIEW PRINT:LOCATE 23
1880 COLOR 0,7:GOTO 1120
1890 :REM'
1900 :REM'.....inductance
1910 COLOR 7,0:VIEW PRINT 23 TO 24:CLS:VIEW PRINT:LOCATE 23
1920 COLOR 0,7:LOCATE 23
1930 INPUT " ENTER: Inductance (µH)";L
1940 XL=L*2*PI*FO
1950 LG=W*ATN(XL/ZIN)/(2*PI)
1960 K=LG/W
1970 LOCATE CSRLIN-1:PRINT X$;
1980 LOCATE CSRLIN-1,8:GOTO 1380
1990 GOSUB 2570:GOTO 1550
2000 :REM'
2010 :REM'.....inductive reactance
2020 COLOR 7,0:VIEW PRINT 23 TO 24:CLS:VIEW PRINT:LOCATE 23
2030 COLOR 0,7:LOCATE 23
2040 INPUT " ENTER: Inductance Reactance XL (ohms)";XL
2050 LG=W*ATN(XL/ZIN)/(2*PI)
2060 K=LG/W
2070 LOCATE CSRLIN-1:PRINT X$;
2080 LOCATE CSRLIN-1,8:GOTO 1380
2090 GOSUB 2570:GOTO 1550
2100 :REM'
2110 :REM'.....capacitance
2120 COLOR 7,0:VIEW PRINT 23 TO 24:CLS:VIEW PRINT:LOCATE 23
2130 COLOR 0,7:LOCATE 23
2140 INPUT " ENTER: Capacitance (pF)";C
2150 IF C<=0 THEN LG=0:K=0:GOTO 2190
2160 XC=10^6/(2*PI*FO*C)
2170 LG=W*ATN(ZIN/XC)/(2*PI)
2180 K=LG/W
2190 LOCATE CSRLIN-1:PRINT X$;
2200 LOCATE CSRLIN-1,8:GOTO 1380
2210 GOSUB 2570:GOTO 1550
2220 :REM'
2230 :REM'.....capacitive reactance
2240 COLOR 7,0:VIEW PRINT 23 TO 24:CLS:VIEW PRINT:LOCATE 23
2250 COLOR 0,7:LOCATE 23
2260 INPUT " ENTER: Capacitance Reactance XC (ohms)";XC
2270 IF XC<=0 THEN K=0.25!:LG=W/4:GOTO 2300
2280 LG=W*ATN(ZIN/XC)/(2*PI)
2290 K=LG/W
2300 LOCATE CSRLIN-1:PRINT X$;
2310 LOCATE CSRLIN-1,8:GOTO 1380
2320 GOSUB 2570:GOTO 1550
2330 :REM'
2340 :REM'.....diagram
2350 J=14
2360 COLOR 0,7
2370 LOCATE ,J:PRINT "                                                      "
2380 LOCATE ,J:PRINT " ╒══shield══════════════╕    ╒══shield══════════════╕ "
2390 LOCATE ,J:PRINT " ────line────┬───────────    ────line────┬─────────── "
2400 LOCATE ,J:PRINT " ╘═════════╤═│═╤════════╛    ╘═════════╤═│═╤════════╛ "
2410 LOCATE ,J:PRINT "           ║ │ ║                       ║ │ ║          "
2420 LOCATE ,J:PRINT "           ║ │ ║                       ║ │ ║          "
2430 LOCATE ,J:PRINT "           ║ │ ║                       ║ │ ║          "
2440 LOCATE ,J:PRINT "           ║ │ ║                      ┌╢ │ ║          "
2450 LOCATE ,J:PRINT "           ╙─│─╜                      │╙─│─╜          "
2460 LOCATE ,J:PRINT "                                      └──┘            "
2470 LOCATE ,J:PRINT "         OPEN STUB                 SHORTED STUB       "
2480 LOCATE ,J:PRINT "                                                      "
2490 COLOR 7,0
2500 RETURN
2510 :REM'
2520 :REM'.....end
2530 GOSUB 2570
2540 ERASE C$:GOTO 300   :REM'start
2550 END
2560 :REM'
2570 :REM'PRT
2580 GOSUB 2640:LOCATE 25,18:COLOR 0,2
2590 PRINT " Press #»▌1=Print▌2=Print/Form Feed▌3=Continue";:COLOR 7,0
2600 Z$=INKEY$:IF Z$<"1"OR Z$>"3"THEN 2600 :ELSE GOSUB 2640
2610 IF Z$="3"THEN RETURN
2620 FOR I%=1 TO 24:FOR J%=1 TO 80:LPRINT CHR$(SCREEN(I%,J%));:NEXT J%:NEXT I%
2630 IF Z$="2"THEN LPRINT CHR$(12) :ELSE 2580
2640 LOCATE 25,1:PRINT STRING$(80,32);:RETURN
