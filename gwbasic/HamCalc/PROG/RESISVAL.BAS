10 :REM'RESISVAL - Standard Resistor Values - 26 OCT 95 rev. 06 DEC 97
20 COLOR 0,2,1:CLS
30 CLS:COLOR 7,0:KEY OFF
40 IF EX$=""THEN EX$="EXIT"
50 DIM R(24)
60 DATA 10,11,12,13,15,16,18,20,22,24,27,30,33,36,39,43,47,51,56,62,68,75,82,91
70 FOR Z=1 TO 24:READ R(Z):NEXT Z
80 X$="  5%&10%  5% only "
90 COLOR ,2:PRINT SPC(4);:COLOR 14,6
100 FOR X=1 TO 4:PRINT X$;:NEXT X:PRINT ""
110 M=0.10000000149011612!:U$="###.#":Z$=" Ω  ":GOSUB 250
120 M= 1:U$="#####":Z$=" Ω  ":GOSUB 250
130 M=10:U$="#####":Z$=" Ω  ":GOSUB 250
140 M=0.10000000149011612!:U$="###.#":Z$=" K  ":GOSUB 250
150 M= 1:U$="#####":Z$=" K  ":GOSUB 250
160 M=10:U$="#####":Z$=" K  ":GOSUB 250
170 M=0.10000000149011612!:U$="###.#":Z$=" meg":GOSUB 250
180 M= 1:U$="#####":Z$=" meg":GOSUB 250
190 COLOR 14,6
200 PRINT TAB(16);" S T A N D A R D   R E S I S T O R   V A L U E S   ";
210 COLOR ,0
220 GOSUB 360
230 CLS:RUN EX$
240 :REM'
250 :REM'.....print
260 FOR Z= 1 TO 24
270 IF Z=1 OR Z=9 OR Z=17 THEN COLOR 2:PRINT "████";
280 IF Z/2=INT(Z/2)THEN COLOR 0,7 :ELSE COLOR 15,1
290 PRINT USING U$;M*R(Z);:PRINT Z$;
300 IF Z=8 OR Z=16 THEN PRINT ""
310 IF Z$=" meg"AND M*R(Z)=22 THEN COLOR 7,0:GOTO 340
320 NEXT Z
330 PRINT ""
340 RETURN
350 :REM'
360 :REM'PRT
370 KEY OFF:GOSUB 440:LOCATE 25,5:COLOR 0,2
380 PRINT " Send this page to:(1)Printer Queue? (2)Printout? ";
390 PRINT "(3)Next page? (1/2/3)";:COLOR 7,0
400 Z$=INKEY$:IF Z$<"1"OR Z$>"3"THEN 400 :ELSE GOSUB 440
410 IF Z$="3"THEN RETURN
420 FOR I%=1 TO 24:FOR J%=1 TO 80:LPRINT CHR$(SCREEN(I%,J%));:NEXT J%:NEXT I%
430 IF Z$="2"THEN LPRINT CHR$(12) :ELSE 370
440 LOCATE 25,1:PRINT STRING$(80,32);:RETURN
